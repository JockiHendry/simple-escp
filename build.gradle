buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.4.1'
    }
}

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'java-library-distribution'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.7
version = '0.4'
group = 'jockihendry'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'javax.json', name: 'javax.json-api', version: '1.0'
    runtime group: 'org.glassfish', name: 'javax.json', version: '1.0.4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

distributions {
    main {
        baseName = 'simple-escp'
    }
}

checkstyle {
    sourceSets = [sourceSets.main]
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

test {
    useJUnit {
        excludeCategories 'simple.escp.category.RequirePrinterCategory'
    }
}

task testRequirePrinter(type: Test) {
    useJUnit {
        includeCategories 'simple.escp.category.RequirePrinterCategory'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    publish = false
    pkg {
        repo = 'maven'
        userOrg = 'jockihendry'
        name = 'simple-escp'
        licenses = ['Apache-2.0']
    }
}

